#+NAME: hiiren-painallukset-aaltoliike
#+BEGIN_SRC processing :exports none
  int vasenViivaX; // vasemmanpuoleisimman viivan x-koordinaatti
  final int VIIVAVALI = 10; // viivojen väli
  final int ESTE_HALKAISIJA = 100; // esteen halkasija (y-akselin suuntainen)
  float ympyraX, ympyraY; // esteympyrän keskipisteen x- ja y-koordinaatit

  void setup ()
  {
    size (600, 400);
    colorMode (HSB, 100);
    background (0);
    fill (0);

    vasenViivaX = 0;

    // asetetaan alussa ympyrän koordinaateille negatiiviset
    // koordinaatit, jolloin ympyrää ei piirretä
    ympyraX = -1;
    ympyraY = -1;
  }

  void draw ()
  {
    background (0);

    // piirretään viivat
    stroke (100);
    for (int viivaX = vasenViivaX; viivaX < width; viivaX += VIIVAVALI)
      line (viivaX, 0, viivaX, height);

    // kasvatetaan vasemmanpuoleisimman viivan x-koordinaattia
    vasenViivaX++;
    if (vasenViivaX > VIIVAVALI)
      vasenViivaX = 0;

    // piirretään ympyrä sekä sen "suojaama" osa piirtoikkunasta
    // suorakulmiona
    if (ympyraX >= 0)
    {
      noStroke ();
      ellipse (ympyraX, ympyraY, ESTE_HALKAISIJA, ESTE_HALKAISIJA);
      float sade = ESTE_HALKAISIJA / 2.0;
      rect (ympyraX, ympyraY - sade, width - ympyraX, ESTE_HALKAISIJA);
    }
  }

  void mousePressed ()
  {
    ympyraX = mouseX;
    ympyraY = mouseY;
  }
#+END_SRC
