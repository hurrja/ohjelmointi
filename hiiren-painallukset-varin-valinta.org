#+NAME: hiiren-painallukset-varin-valinta
#+BEGIN_SRC processing :exports none
  final int VARIT_LEVEYS = 200; // palettien leveys kuvassa
  float valittuSavy; // piirretyn ympyrän värisävy
  float ympyraX; // ympyrän keskipisteen x-koordinaatti

  void setup ()
  {
    size (600, 100);
    colorMode (HSB, 100);

    valittuSavy = 0; // alkuarvo värisävylle

    // ympyrä piirretään paletin oikealle puolelle jäävän osan
    // keskikohtaan
    ympyraX = (VARIT_LEVEYS + width) / 2.0; 
  }

  void draw ()
  {
    background (0);

    // piirretään paletti; kutakin värisävyä 0, 1, ..., 100 vastaava
    // viiva piirretään vaakatasoon vastaavaan y-koordinaattiin
    
    for (int savy = 0; savy < 100; savy++)
    {
      stroke (savy, 100, 100);
      line (0, savy, VARIT_LEVEYS, savy); // viivan y-koordinaatti on värisävy
    }

    // piirretään ympyrä
    final float HALKAISIJA = height;
    noStroke ();
    fill (valittuSavy, 100, 100);
    ellipse (ympyraX, height / 2.0, HALKAISIJA, HALKAISIJA);
  }

  void mousePressed ()
  {
    // ympyrän värisävy on suoraan hiiren kursorin y-koordinaatti, koska
    // paletti piirrettiin siten
    valittuSavy = mouseY;
  }
#+END_SRC
